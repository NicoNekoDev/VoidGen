buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'io.github.goooler.shadow' version "8.1.7"
}

group = project.plugin_group as Object
version = project.plugin_version as Object

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name 'arim-mvn-lgpl3'
        url 'https://mvn-repo.arim.space/lesser-gpl3/'
    }
}

dependencies {
//    implementation 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.18.1-R0.1-SNAPSHOT'
    implementation 'space.arim.morepaperlib:morepaperlib:0.4.3'
}

processResources {
    def properties = [version: project.plugin_version, name: project.plugin_name,
                      group  : project.plugin_group, website: project.plugin_website,
                      author : project.plugin_author]
    inputs.properties properties
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand properties
    }
}

shadowJar {
    archiveFileName.set("${project.name}-${project.version}.jar")
    relocate 'space.arim.morepaperlib', 'de.xtkq.voidgen.lib.morepaperlib'
}

tasks.build {
    dependsOn shadowJar
}